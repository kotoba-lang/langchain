(ns langchain.persist-test
  (:require [clojure.test :refer [deftest is testing]]
            [langchain.db :as db]
            [langchain.persist :as persist]))

(def schema
  {:person/id {:db/unique :db.unique/identity}})

(defn memory-event-log []
  (let [state (atom {:seq 0 :streams {}})]
    {:append (fn [stream event]
               (let [result (volatile! nil)]
                 (swap! state
                        (fn [{:keys [seq] :as current}]
                          (let [event' (assoc event :seq (inc seq))]
                            (vreset! result event')
                            (-> current
                                (assoc :seq (inc seq))
                                (update-in [:streams stream] (fnil conj []) event')))))
                 @result))
     :read (fn [stream since]
             (->> (get-in @state [:streams stream])
                  (filter #(> (:seq %) (or since 0)))
                  vec))}))

(deftest transact-persists-and-replays
  (let [host (memory-event-log)
        persistence (persist/scoped host "thread-1")]
    (testing "a fresh conn with no prior events starts empty"
      (let [conn (db/create-conn schema persistence)]
        (is (= [] (db/q '[:find [?e ...] :where [?e :person/id _]] (db/db conn))))))
    (testing "new connections replay the host event log"
      (let [conn (db/create-conn schema persistence)]
        (db/transact! conn [{:db/id -1 :person/id "alice" :person/name "Alice"}]))
      (let [conn (db/create-conn schema persistence)]
        (is (= "Alice"
               (db/q '[:find ?n . :where [?e :person/id "alice"] [?e :person/name ?n]]
                     (db/db conn))))))))

(deftest distinct-streams-are-isolated
  (let [host (memory-event-log)
        a (db/create-conn schema (persist/scoped host "a"))
        b (db/create-conn schema (persist/scoped host "b"))]
    (db/transact! a [{:db/id -1 :person/id "alice" :person/name "Alice"}])
    (is (= #{"Alice"}
           (set (db/q '[:find [?n ...] :where [_ :person/name ?n]] (db/db a)))))
    (is (= #{}
           (set (db/q '[:find [?n ...] :where [_ :person/name ?n]] (db/db b)))))))
